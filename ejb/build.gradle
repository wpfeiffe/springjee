apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'

configurations {
    endorsed
    compile.extendsFrom endorsed
    provided
}

sourceSets.main.compileClasspath += configurations.provided

repositories {
    mavenCentral()
}

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '2.1.0'
    provided group: 'org.codehaus.groovy', name: 'groovy', version: '2.1.0'
    provided 'org.jboss.spec:jboss-javaee-6.0:1.0.0.Final'
    provided 'org.springframework.data:spring-data-jpa:1.2.0.RELEASE'
    provided 'org.infinispan:infinispan-core:5.1.7.Final'
    endorsed 'org.jboss.spec.javax.annotation:jboss-annotations-api_1.1_spec:1.0.0.Final'
    compile project(':jpa') {
        transitive = false
    }
    testCompile group: 'junit', name: 'junit', version: '4.7'
}

// optional: if using 'idea' plugin
idea {
  module{
    scopes.PROVIDED.plus += configurations.provided
  }
}

// optional: if using 'eclipse' plugin
eclipse {
  classpath {
    plusConfigurations += configurations.provided
  }
}

task stageEndorsed(type: Sync) {
    from configurations.endorsed
    into temporaryDir
}
tasks.withType(Compile) {
    dependsOn stageEndorsed
    options.compilerArgs << "-Djava.endorsed.dirs=${stageEndorsed.destinationDir}"
}
